#!/usr/bin/env perl
#
# Author: petr.danecek@sanger
#
# Taken from vcftools: http://vcftools.sourceforge.net/index.html
#
# License information reproduced here:
#
# License:
#     The program package is released under the GNU Lesser General Public
#     License version 3.0 (LGPLv3).
#
# Credits:
#     Adam Auton        (cpp executable)
#     Petr Danecek      (perl)
#     Anthony Marcketta (cpp executable)
#
# Compiling:
#     To compile and install VCFtools, you should type 'make' in the
#     vcftools folder. The perl scripts and cpp executable will be
#     installed in the /vcftools_(version_num)/bin/ folder. It is
#     recommended that you add this folder to you PATH.
#
# Documentation:
#     The latest version of the documentation and examples of usage can be
#     found in the website subdirectory or go online:
#          http://vcftools.sourceforge.net/docs.html
#
# Getting Help:
#     The best way to get help regarding VCFtools is to email the mailing
#     list: vcftools-help@lists.sourceforge.net
#
# Citation:
#     If you make use of VCFtools in your research, we would appreciate a
#     citation of the following paper:
#
#     The Variant Call Format and VCFtools,
#     Petr Danecek, Adam Auton, Goncalo Abecasis, Cornelis A. Albers,
#     Eric Banks, Mark A. DePristo, Robert Handsaker, Gerton Lunter,
#     Gabor Marth, Stephen T. Sherry, Gilean McVean, Richard Durbin
#     and 1000 Genomes Project Analysis Group, Bioinformatics, 2011
#
#     http://dx.doi.org/10.1093/bioinformatics/btr330
#
#

use strict;
use warnings;
use Carp;

my $additional_sort_options="";
my $opts = parse_params();
sort_vcf($opts);

exit;

#--------------------------------

sub error
{
    my (@msg) = @_;
    if ( scalar @msg )
    {
        croak @msg;
    }
    die
        "Usage: vcf-sort > out.vcf\n",
        "       cat file.vcf | vcf-sort > out.vcf\n",
        "Options:\n",
        "   -s, --sort-options              Options to add to the 'sort' command\n",
        "   -c, --chromosomal-order         Use natural ordering (1,2,10,MT,X) rather then the default (1,10,2,MT,X). This requires\n",
        "                                       new version of the unix \"sort\" command which supports the --version-sort option.\n",
        "   -h, -?, --help                  This help message.\n",
        "\n";
}

sub parse_params
{
    my $opts = {};
    while (my $arg=shift(@ARGV))
    {
        if ( $arg eq '-c' || $arg eq '--chromosomal-order' ) { $$opts{chromosomal_order}=1; next; }
        if ( $arg eq '-?' || $arg eq '-h' || $arg eq '--help' ) { error(); }
        if ( $arg eq '-s' || $arg eq '--sort-options') { $additional_sort_options=shift @ARGV; next; }
        if ( -e $arg ) { $$opts{file}=$arg; next }
        error("Unknown parameter \"$arg\". Run -h for help.\n");
    }
    return $opts;
}

sub sort_vcf
{
    my ($opts) = @_;

    my $fh;
    if ( exists($$opts{file}) )
    {
        if ( $$opts{file}=~/\.gz$/i )
        {
            open($fh,"gunzip -c $$opts{file} |") or error("$$opts{file}: $!");
        }
        else
        {
            open($fh,'<',$$opts{file}) or error("$$opts{file}: $!");
        }
    }
    else { $fh = *STDIN; }

    my $sort_opts = check_sort_options($opts);
    my $cmd = "sort $additional_sort_options $sort_opts -k2,2n";
    open(my $sort_fh,"| $cmd") or error("$cmd: $!");

    my $unflushed = select(STDOUT);
    $| = 1;
    while (my $line=<$fh>)
    {
        if ( $line=~/^#/ ) { print $line; next; }
        print $sort_fh $line;
        last;
    }
    select($unflushed);
    while (my $line=<$fh>)
    {
        print $sort_fh $line;
    }
}

sub check_sort_options
{
    my ($opts) = @_;
    if ( !$$opts{chromosomal_order} ) { return '-k1,1d' }

    my @has_version_sort = `sort --help | grep -- --version-sort`;
    if ( scalar @has_version_sort ) { return '-k1,1V'; }

    error("Old version of sort command installed, please run without the -c option.\n");
    return '-k1,1d';
}

